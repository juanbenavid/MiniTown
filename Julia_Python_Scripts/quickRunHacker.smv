
MODULE main

DEFINE

pumpFlow0 := 0;
pumpFlow1 := 113.699 + 6.735*demand - 1.35* (tank/100);
pumpFlow2 := 181.44 + 18.356*demand - 2.1*(tank/100);

tankFlow0 := demand * 246 - pumpFlow0;
tankFlow1 := demand * 246 - pumpFlow1;
tankFlow2 := demand * 246 - pumpFlow2;

dT0 := tankFlow0 * 0.004678 *100;
dT1 := tankFlow1 * 0.004678 *100;
dT2 := tankFlow2 * 0.004678 *100;


VAR
  pump1  : {on,off};
  pump2  : {on,off};
  demand : real;
  tank   : real;
  time   : integer;

  actuator1: {on,off};
  actuator2: {on,off};
  tankSensor: real;
  falseRead: {active,waiting};
  attackTime: 1..3;
  reading:{640,460,300,50};


ASSIGN

init(falseRead) := waiting;
init(time) := 0;

    
init(tank) :=300; 
init(pump1) :=on; 
init(pump2) :=off; 
init(tankSensor) :=300; 
init(actuator1) :=on; 
init(actuator2) :=off; 


next(pump1) := next(actuator1);
next(pump2) := next(actuator2);

next(attackTime) := attackTime;
next(reading) := reading;

next(actuator1) := case

	next(tankSensor) <= 400 : on;
	next(tankSensor) >= 630 : off;
	TRUE : actuator1;
esac;

next(actuator2) := case

	next(tankSensor) <= 100 : on;
	next(tankSensor) >= 450 : off;
	TRUE : actuator2;
esac;

next(tankSensor) := case
	
	falseRead = active: reading;
	TRUE: next(tank);
esac;

next(falseRead) := case

	time >= attackTime -1 & time <= attackTime +1: active;
	TRUE: waiting; 

esac;

next(time) := time + 1;
	
INVAR demand <= 1;
INVAR demand >= 0;

TRANS pump1 = off & pump2 = off -> next(tank) = tank - dT0;
TRANS pump1 = on & pump2 = off -> next(tank) = tank - dT1;
TRANS pump1 = on & pump2 = on -> next(tank) = tank - dT2;
TRANS pump1 = off & pump2 = on -> next(tank) = tank - dT1;

LTLSPEC G (time <= 5 -> tank <= 400); -- 0
LTLSPEC G (time <= 5 -> tank <= 550); -- 1
LTLSPEC G (time <= 5 -> tank <= 600); -- 2
LTLSPEC G (time <= 5 -> tank <= 650); -- 3

LTLSPEC G (time <= 5 -> tank >= 300); -- 4
LTLSPEC G (time <= 5 -> tank >= 200); -- 5
LTLSPEC G (time <= 5 -> tank >= 100); -- 6
LTLSPEC G (time <= 5 -> tank >= 25); -- 7
LTLSPEC G (time <= 5 -> tank >= 10); -- 8
    